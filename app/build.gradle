import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
}

def versionMajor = 1
def versionMinor = 2
def versionPatch = 0
def versionCodeSerial = 3

def nav_version = "2.4.1"
def room_version = "2.4.2"
def paging_version = "2.1.2"
def retrofit_version = "2.9.0"

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdk 31

    defaultConfig {
        applicationId "app.sctp"
        minSdk 22
        targetSdk 30
        versionCode versionCodeSerial
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

    }

    viewBinding {
        enabled = true
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all {
                    def timestamp = new SimpleDateFormat("yyyyMMddHHmm").format(new Date())
                    def type = buildType.name
                    def appVersionName = "${applicationId}-v${versionName}"

                    outputFileName = "${appVersionName}-${timestamp}-${type}.apk"
                }
            }
        }
        debug {
            versionNameSuffix '-debug'
            applicationIdSuffix '.dbg'
        }
        all {
            buildConfigField("String", "API_URL", "\"" + localProperties['api.url'] + "\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.auth0.android:jwtdecode:2.0.1'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'

    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    // room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // paging
    implementation "androidx.room:room-paging:$room_version"
    //noinspection GradleDependency
    implementation "androidx.paging:paging-runtime:$paging_version"
}